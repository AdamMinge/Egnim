# -------------------------------------------------------------------------------------------------- #
# --------------------------------- Check that called with component ------------------------------- #
# -------------------------------------------------------------------------------------------------- #
if (NOT egnim_FIND_COMPONENTS)
    message(FATAL_ERROR "find_package(egnim) called with no component")
endif()
# -------------------------------------------------------------------------------------------------- #
# ---------------------------- Set config_name dependent on libraries type ------------------------- #
# -------------------------------------------------------------------------------------------------- #
if(EGNIM_STATIC_LIBRARIES)
    set(config_name "static")
else()
    set(config_name "shared")
endif()
# -------------------------------------------------------------------------------------------------- #
# ------------------------------------ Update requested components --------------------------------- #
# -------------------------------------------------------------------------------------------------- #
set(FIND_egnim_ADDITIONAL_COMPONENTS "")

foreach(component ${egnim_FIND_COMPONENTS})
    list(APPEND FIND_egnim_ADDITIONAL_COMPONENTS ${FIND_egnim_${component}_DEPENDENCIES})
endforeach()

list(APPEND egnim_FIND_COMPONENTS ${FIND_egnim_ADDITIONAL_COMPONENTS})
list(REMOVE_DUPLICATES egnim_FIND_COMPONENTS)
# -------------------------------------------------------------------------------------------------- #
# ----------------------------------- Load targets and dependencies -------------------------------- #
# -------------------------------------------------------------------------------------------------- #
set(targets_config_file ${CMAKE_CURRENT_LIST_DIR}/egnim-${config_name}-targets.cmake)
set(egnim_FOUND TRUE)

if(EXISTS ${targets_config_file})
    include(${targets_config_file})
    include("${CMAKE_CURRENT_LIST_DIR}/egnim-config-dependencies.cmake")

    if (egnim_FOUND)
        foreach (component ${egnim_FIND_COMPONENTS})
            if (TARGET egnim-${component})
                set(egnim_${component}_FOUND TRUE)
            else()
                set(FIND_egnim_ERROR "Found egnim but requested component '${component}' is missing in the config defined in ${EGNIM_DIR}.")
                set(egnim_${component}_FOUND FALSE)
                set(egnim_FOUND FALSE)
            endif()
        endforeach()
    endif()
else()
    set(FIND_egnim_ERROR "Requested egnim configuration (${config_name}) was not found")
    set(egnim_FOUND FALSE)
endif()
# -------------------------------------------------------------------------------------------------- #
# --------------------------------------- Display all errors --------------------------------------- #
# -------------------------------------------------------------------------------------------------- #
if (NOT egnim_FOUND)
    if(egnim_FIND_REQUIRED)
        message(FATAL_ERROR "${FIND_egnim_ERROR}")
    elseif(NOT egnim_FIND_QUIETLY)
        message(STATUS "${FIND_egnim_ERROR}")
    endif()
endif()
# -------------------------------------------------------------------------------------------------- #
